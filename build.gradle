import java.text.SimpleDateFormat

plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'org.sonatype.gradle.plugins.scan' version '1.2.0'
}

group = 'org.sonatype.cs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.webjars:jquery:3.5.1'
	implementation 'org.webjars:bootstrap:4.5.0'

	compile 'com.opencsv:opencsv:5.2'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

nexusIQScan {
  username = 'admin'
  password = 'admin123'
  serverUrl = 'http://localhost:8070'
  applicationId = 'success-metrics'
  stage = 'build'  
  allConfigurations = true 
}

task appclean(type: Delete) {
    delete "${rootDir}/success-metrics.zip/"
	delete "${rootDir}/quickstart-sm"
}

task copydependencies(type: Copy) {
  from configurations.default
  into 'build/dependencies'
}

task bundle(type: Zip) {
   from 'build/libs'
   include '*.jar'
   include 'run.sh'
   archiveName 'success-metrics.zip'
   destinationDir(file(projectDir))
}

task runfile()  {
    doLast {
		mkdir 'quickstart-sm'
        new File('build/libs', "run.sh").text = """
#!/bin/sh
#Version: $version
#Buildtime: ${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
#Application-name: success-metrics
java -jar success-metrics-${version}.jar
"""
    }
}

bundle.dependsOn runfile
runfile.dependsOn appclean