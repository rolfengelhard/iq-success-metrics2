import java.text.SimpleDateFormat

plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'org.sonatype.gradle.plugins.scan' version '1.2.0'
}

group = 'org.sonatype.cs'
version = '4.3'
sourceCompatibility = '8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.webjars:jquery:3.5.1'
	implementation 'org.webjars:bootstrap:4.5.0'

	compile 'com.opencsv:opencsv:5.2'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

nexusIQScan {
  username = 'admin'
  password = 'admin123'
  serverUrl = 'http://localhost:8070'
  applicationId = 'success-metrics'
  stage = 'build'  
  allConfigurations = true 
}

clean.doFirst {
	delete "${rootDir}/success-metrics.zip/"
	delete "${rootDir}/success-metrics"
}

build.doFirst {
	mkdir 'success-metrics'
}

task copydependencies(type: Copy) {
  from configurations.default
  into 'build/dependencies'
}

task release (type: Copy) {
	from 'build/libs', 'release-files'
	include '*.jar'
	include '*.py'
	include '*.json'
	include '*.sh'
	include '*.bat'
	into 'success-metrics'
}

task bundle(type: Zip) {
	from 'success-metrics/'
	archiveName 'success-metrics.zip'
	destinationDir(file(projectDir))
	doLast {
		delete 'success-metrics'
	}
}

task runfile  {
    doLast {
        new File('success-metrics', 'run.sh').text = """
#!/bin/sh
#Version: $version
#Buildtime: ${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
#Application-name: success-metrics
java -jar success-metrics-${version}.jar
"""
    }
}

release.dependsOn runfile
bundle.dependsOn release


